# Maintainer: Jingbei Li <i@jingbei.lli>
pkgname='kaldi'
pkgdesc='Speech recognition research toolkit'
pkgver=5.3.r7679.a3441c243
pkgrel=1
makedepends=('git' 'wget' 'python' 'python2' 'subversion' 'gcc6' "cuda")
depends=('python2' 'kaldi-openfst' 'openblas-lapack' 'sed')
optdepends=('cuda: For GPU support'
	'kaldi-irstlm'
	'kaldi-kaldi_lm'
	'kaldi-sctk'
	'kaldi-sph2pipe'
	'kaldi-srilm'
	)
arch=('x86_64' 'i686')
url='https://github.com/kaldi-asr/kaldi'
license=('APACHE')
source=("git+${url}")
sha256sums=('SKIP')

pkgver () {
	cd "${pkgname}"
	(
	set -o pipefail
	echo -n `cat src/.version`.
	git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare(){
	cd $srcdir/$pkgname
	find . -name '*.py' -exec sed '1s/python/python2/' -i {} \;

	if (pacman -Q cuda &> /dev/null); then
		msg2 "Compiling with CUDA support"
		_cuda_config_opts="--cudatk-dir=/opt/cuda"
	else
		msg2 "Compiling _without_ CUDA support"
	fi
}

build () {
	cd $srcdir/$pkgname/src
	CXX=g++-6 \
	./configure $_cuda_config_opts \
		--shared \
		--fst-root=/opt/kaldi/tools/openfst \
		--openblas-root=/usr \
		--threaded-math=yes
	make depend
	make
}

package () {
	cd  $srcdir/$pkgname

	for i in src/lib/*.so
	do
		mv `realpath $i` $i
	done
	rm -f src/*/*.{cc,cu,o,a,orig}
	rm -r src/{doc,feat/test_data,lm/examples,lm/test_data,makefiles,onlinebin,probe}
	find src \
		-name 'Makefile*' \
		-or -name 'README' \
		-exec rm {} \;
	find src \
		-name '*.mk' \
		-not -name 'kaldi.mk' \
		-exec rm {} \;
	find src -maxdepth 1 -type f -not -name 'kaldi.mk' -exec rm {} \;
	rm -r tools/{ATLAS_headers,CLAPACK,INSTALL,Makefile}

	sed "s|$srcdir|/opt|g" -i `grep $srcdir . -rIl`
	find . -name 'path.sh' -exec sed 's?^\(export KALDI_ROOT\)=.*$?\1=/opt/'$pkgname'?' -i {} \;
	echo "export OPENFST=/opt/kaldi/$(find tools -type d -name 'openfst*')" >> tools/env.sh
	echo 'export LD_LIBRARY_PATH=${LD_LIBRARY_PATH:-}:${OPENFST}/lib' >> tools/env.sh
	echo "export IRSTLM=/opt/kaldi/tools/irstlm" >> tools/env.sh
	echo "export export PATH=${PATH}:${IRSTLM}/bin" >> tools/env.sh

	install -dm755 "$pkgdir"/etc/ld.so.conf.d/
	echo "/opt/$pkgname/src/lib" > "$pkgdir"/etc/ld.so.conf.d/$pkgname.conf

	mkdir -p $pkgdir/opt/$pkgname
	cp -r src egs tools $pkgdir/opt/$pkgname
}
