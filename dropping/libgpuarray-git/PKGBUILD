# Maintainer: David McInnis <dave@dave3.xyz>
# Contributor:  VirtualTam   <virtualtam@flibidi.net>
# Contributor: Jon Gjengset <jon@thesquareplanet.com>
pkgname=libgpuarray-git
_gitname=libgpuarray
pkgver=0.7.6.20.g9cec614
pkgrel=2
pkgdesc="Library to manipulate tensors on the GPU"
arch=('i686' 'x86_64')
url="https://github.com/Theano/libgpuarray"
license=('MIT')
depends=('glibc' 'python')
makedepends=('git' 'cmake' 'python')
provides=("${_gitname}")
source=("git+https://github.com/Theano/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd ${_gitname}
  git describe --always --tags | sed -e 's/-/./g' -e 's/v//g'
}

prepare() {
  cd ${_gitname}
  rm -rf _build
  mkdir _build
  cd _build
  cmake .. -DCMAKE_BUILD_TYPE=Release "-DCMAKE_INSTALL_PREFIX=${pkgdir}/usr"
}

build() {
  cd ${_gitname}/_build
  make
}

package() {
  cd ${_gitname}/_build
  make install
  install -D -m644 "../LICENSE" "${pkgdir}/usr/share/licenses/libgpuarray-git/LICENSE"
}
