# Maintainer: Jingbei Li <i@jingbei.lli>
pkgdesc='A fast parallel implementation of CTC, on both CPU and GPU.'
pkgname='torch7-warp-ctc'
pkgver=v1.0.r19.g49b37ba
pkgrel=1
makedepends=('cmake' 'git' 'torch7-cutorch-git' "torch7-cutorch-git")
depends=('torch7-git>=r819')
optdepends=('torch7-cutorch-git:	For GPU support')
arch=('x86_64' 'i686')
url='https://github.com/baidu-research/warp-ctc'
license=('APACHE')
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')

pkgver () {
	cd "${pkgname}"
	(
		set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build () {
	cd "${pkgname}"
	sed 's/luajit/luajit-5.1/g' -i CMakeLists.txt
	# Uncomment this line if you are using a Pascal GPU
	# sed -e 's/compute_[0-9]\{2\}/compute_61/g' -e 's/sm_[0-9]\{2\}/sm_61/g' -i CMakeLists.txt
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_C_COMPILER=gcc-5 -DCMAKE_BUILD_TYPE=Release
	make
}

package () {
	cd "${pkgname}"
	# Move Lua C module
	mkdir -p "${pkgdir}/usr/lib/lua/5.1"
	install -Dm755 "libwarpctc.so" "${pkgdir}/usr/lib/"
	install -Dm755 "libwarp_ctc.so" "${pkgdir}/usr/lib/lua/5.1/"

	# Move pure Lua modules
	mkdir -p "${pkgdir}/usr/share/lua/5.1/warp_ctc"
	install -Dm644 "torch_binding/init.lua" "${pkgdir}/usr/share/lua/5.1/warp_ctc"
}
