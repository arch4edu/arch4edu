# Maintainer:  AppleBloom <rat.o.drat@gmail.com> 

_pkgname="python2-pymavlink"
pkgname="$_pkgname-git"
pkgver=r2192.0e949ae
pkgrel=1
pkgdesc="Python bindings for MAVLink micro air vehicle marshalling / communication library."
arch=('any')
license=('GPL3')
url='http://qgroundcontrol.org/mavlink/start'
depends=('python2-pyserial' "python2-future")
makedepends=('git' 'python2-setuptools' "python2-setuptools" "python2-lxml")
provides=($_pkgname)
conflicts=($_pkgname)
source=("git+https://github.com/mavlink/mavlink.git"
        "git+https://github.com/ArduPilot/pymavlink.git")
sha256sums=('SKIP' 'SKIP')

prepare() {
  cd "mavlink"
  git submodule init
  git config submodule.pymavlink.url "$srcdir/pymavlink"
  git submodule update
}

pkgver() {
  cd "mavlink/pymavlink"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "mavlink/pymavlink"
  python2 setup.py build
}

package() {
  cd "mavlink/pymavlink"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
