# Maintainer: John Lane <archlinux at jelmail dot com>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
pkgname=targetcli-fb
pkgver=3.0.1
pkgrel=2
pkgdesc="free branch of the targetcli LIO administration shell (iSCSI + Co)"
arch=('any')
url="https://github.com/open-iscsi/targetcli-fb"
license=('Apache-2.0')
depends=(
  'python'
  'python-rtslib-fb'
  'python-configshell-fb'
  'python-gobject'
)
makedepends=(
  'python-build'
  'python-hatch-vcs'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
optdepends=()
provides=('targetcli')
backup=()
options=()
install=
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/open-iscsi/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('e4ed5b1911ba8858fcf8fe39580462b7b645672f836b195fba0549fb5f9532bf5223a7034bf92c14e5d01c106e388d8ec942cacac421dfd1271f9916ac47f4c0')


build() {
  cd "$srcdir/$pkgname-$pkgver"
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
  gzip --stdout targetcli.8 > "targetcli.8.gz"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -d -m 750 "${pkgdir}/etc/target"
  install -D -m 644 targetcli.8.gz "$pkgdir/usr/share/man/man8/targetcli.8.gz"
  install -D -m 644 systemd/targetclid.service "$pkgdir/usr/lib/systemd/system/targetclid.service"
  install -D -m 644 systemd/targetclid.socket "$pkgdir/usr/lib/systemd/system/targetclid.socket"
  install -D -m 644 README.md "$pkgdir/usr/share/doc/targetcli/README.md"
  install -D -m 644 THANKS "$pkgdir/usr/share/doc/targetcli/THANKS"
}
