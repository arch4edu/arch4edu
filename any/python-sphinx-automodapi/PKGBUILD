# Maintainer: Astro Benzene <universebenzene at sina dot com>

pkgbase=python-sphinx-automodapi
_pname=${pkgbase#python-}
_pyname=${_pname//-/_}
pkgname=("python-${_pname}" "python-${_pname}-doc")
pkgver=0.22.0
pkgrel=1
pkgdesc="Sphinx extension for generating API documentation."
arch=('any')
url="https://sphinx-automodapi.readthedocs.io"
license=('BSD-3-Clause')
makedepends=('python-setuptools-scm>=8.0.0'
             'python-build'
             'python-installer'
             'python-sphinx'
             'python-sphinx_rtd_theme')  # wheel required by new setuptools
checkdepends=('python-pytest'
#             'python-pytest-xdist'
              'cython'
              'graphviz') # sphinx already in makedepends
source=("https://files.pythonhosted.org/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('9a6745d606eb948321f0bb7bd4511dc6d8481da8819caa1604d7138910173b38')

get_pyver() {
    python -c "import sys; print('$1'.join(map(str, sys.version_info[:2])))"
}

build() {
    cd ${srcdir}/${_pyname}-${pkgver}
    python -m build --wheel --no-isolation

    msg "Building Docs"
    PYTHONPATH="../build/lib" make -C docs html
}

check() {
    cd ${srcdir}/${_pyname}-${pkgver}

    #ln -rs ${srcdir}/${_pyname}-${pkgver}/${_pyname/-/_}*egg-info \
    #    build/lib/${_pyname/-/_}-${pkgver}-py$(get_pyver .).egg-info
    #PYTHONPATH="build/lib" pytest ${_pyname/-/_} || warning "Tests failed"
    # need docutils<0.22
    pytest || warning "Tests failed" # -vv -l -ra --color=yes -o console_output_style=count -p xdist -n 4 #
}

package_python-sphinx-automodapi() {
    depends=('python-sphinx>=5'
             'python-packaging')
    optdepends=('python-sphinx_rtd_theme: rtd'
                'python-sphinx-automodapi-doc: Documentation for sphinx-automodapi')
    cd ${srcdir}/${_pyname}-${pkgver}

    install -D -m644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -D -m644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}

package_python-sphinx-automodapi-doc() {
    pkgdesc="Documentation for sphinx-automodapi"
    cd ${srcdir}/${_pyname}-${pkgver}/docs/_build

    install -D -m644 -t "${pkgdir}/usr/share/licenses/${pkgname}" ../../LICENSE.rst
    install -d -m755 "${pkgdir}/usr/share/doc/${pkgbase}"
    cp -a html "${pkgdir}/usr/share/doc/${pkgbase}"
}
makedepends+=("python-pip")
