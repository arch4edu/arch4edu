# Maintainer: Jeremy Gust <jeremy AT plasticsoup DOT net>
# Contributor: Theo Ouzhinski <touzhinski at gmail dot com>
pkgname=python-yamlloader
pkgver=1.4.1
pkgrel=3
pkgdesc='Loaders and dumpers for PyYAML'
arch=('any')
url=https://github.com/Phynix/yamlloader
license=('MIT')
depends=('python' 'python-pyaml' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("${pkgname#python-}-$pkgver::${url}/archive/refs/tags/$pkgver.tar.gz")
sha256sums=("c4b9f6c7ab62202aee9ef30814953c8d35ea8984018b98c7f064d536e197c1bf")

build() {
	cd "${pkgname#python-}-$pkgver"
	python -m build --wheel --no-isolation
}

package() {
	cd "${pkgname#python-}-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 README.rst "${pkgdir}/usr/share/doc/$pkgname/README.rst"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
makedepends+=("python-setuptools")
