# Maintainer: Jingbei Li <i@jingbei.li>
# Contributor: Jack Allnutt <jack@allnutt.eu>
pkgname=kiwiirc-git
pkgver=1.7.1.r63.g50f5d630
pkgrel=1
pkgdesc="A hand-crafted web-based IRC client that you can enjoy"
arch=('any')
url="https://kiwiirc.com/"
license=('APACHE')
makedepends=('git' 'nodejs-lts-hydrogen' 'yarn')
install='kiwiirc.install'
source=(git+https://github.com/kiwiirc/kiwiirc)
md5sums=('SKIP')

pkgver () {
    cd "${pkgname%-git}"
    (
        set -o pipefail
        git describe --tags 2>/dev/null | sed -e 's/\([^-]*-g\)/r\1/;s/-/./g' -e 's/^v//'||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "${srcdir}/${pkgname%-git}"
    yarn install
    yarn run build
}

package() {
    cd "${srcdir}/${pkgname%-git}"

    mkdir -p $pkgdir/usr/share/webapps/
    cp -r dist $pkgdir/usr/share/webapps/kiwiirc
    sed 's/welcome/personal/' -i $pkgdir/usr/share/webapps/kiwiirc/static/config.json
}
