# Maintainer: XZS <d dot f dot fischer at web dot de>
pkgname=python-grip-git
pkgver=4.5.2.r543.ce933cc
pkgrel=1
pkgdesc="Preview GitHub Markdown files like Readme locally before committing them."
arch=('any')
url="https://github.com/joeyespo/grip"
license=('MIT')
depends=('python-requests' 'python-flask' 'python-docopt'
         'python-markdown' 'python-pygments' 'python-path-and-address')
makedepends=('python-setuptools')

makedepends+=('git')
source+=("${_gitname:=${pkgname%-git}}::${_giturl:-git+$url}")
for integ in $(get_integlist)
do
  typeset -n array="${integ}sums"
  array+=('SKIP')
done
provides+=("$_gitname=$pkgver")
conflicts+=("$_gitname")

pkgver() {
  cd "$_gitname"
  grep -Po "(?<=version=').*(?=',)" setup.py | tr '\n' '.'
printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$_gitname"
  python setup.py install --root="$pkgdir/" --optimize=1
}
