# Maintainer: acxz <akashpatel2008@yahoo.com>
# Contributor: K. Morton <pryre.dev@outlook.com>
# Contributor: Anselmo L. S. Melo <anselmo.melo@intel.com>
pkgname=qgroundcontrol
pkgver=4.1.0
pkgrel=1
pkgdesc="Micro air vehicle ground control station."
arch=('x86_64')
url="https://github.com/mavlink/qgroundcontrol"
license=('GPL3')

# Git commit hash for version-specific submodules
pkgver_gps='5afc11588b2b6ec0861ecf9ef673c7a9e2109129' # src/GPS/Drivers
pkgver_mavlink='5637057af5ab8ec5667e3f8e5f8c73d3bc60eed8' # libs/mavlink/include/mavlink/v2.0
pkgver_aossl='3aaff1bd9e35047abdb363239bb3e3c114d07ea1' # libs/OpenSSL/android_openssl
pkgver_gst='9d782fad9dc0384ba86ecae64511c193f6149f93' # libs/qmlglsink/gst-plugins-good

depends=('bzip2'
         'dbus'
         'flac'
         'gst-plugins-base-libs'
         'libasyncns'
         'libffi'
         'libgcrypt'
         'libgpg-error'
         'libogg'
         'libsndfile'
         'libsystemd'
         'libunwind'
         'libx11'
         'libxau'
         'libxcb'
         'libxdmcp'
         'libxext'
         'lz4'
         'orc'
         'pcre'
         'sdl2'
         'xz'
         'zlib'
         'icu'
         'qt5-speech'
         'qt5-multimedia'
         'qt5-serialport'
         'qt5-charts'
         'qt5-quickcontrols'
         'qt5-quickcontrols2'
         'qt5-location'
         'qt5-svg'
         'qt5-graphicaleffects'
         'qt5-tools'
         'qt5-wayland'
         'qt5-x11extras'
)

makedepends=('git' 'qt5-base' 'qt5-tools' 'qt5-wayland' 'qt5-x11extras')

source=("qgroundcontrol-${pkgver}.tar.gz::https://github.com/mavlink/qgroundcontrol/archive/v${pkgver}.tar.gz"
        "gps-drivers-qgc${pkgver}.tar.gz::https://github.com/PX4/GpsDrivers/archive/${pkgver_gps}.tar.gz"
        "mavlink-v2.0-qgc${pkgver}.tar.gz::https://github.com/mavlink/c_library_v2/archive/${pkgver_mavlink}.tar.gz"
        "aossl-qgc${pkgver}.tar.gz::https://github.com/Auterion/android_openssl/archive/${pkgver_aossl}.tar.gz"
        "gst-plugins-good-qgc${pkgver}.tar.gz::https://github.com/mavlink/gst-plugins-good/archive/${pkgver_gst}.tar.gz"
        "libicudata-qgc.patch::https://patch-diff.githubusercontent.com/raw/mavlink/qgroundcontrol/pull/9332.patch"
)

sha256sums=('9a9c67e293290e296aa6716441f8ff5b7d9006286e2a6d8c145e283eab75e310'
            '9d158a4f611d605714ec0b6f643ab9ee02be6f64d67d8ba823fbc4bb29d853f2'
            '9f251211db18eb0e1b636033a0eae7344ff88718cc61ae46249ca628e906de2e'
            '93598e63fbbd86fec5e15f2596bba8b1f1654c854a99222099516933fd22a118'
            'd5aad13c8eff7f3cce75c8cf3bbf6ac592ac82455e666dccd17cf006deec3e55'
            'SKIP'
)

prepare() {
  gpsdir="PX4-GPSDrivers-${pkgver_gps}"
  mavlinkdir="c_library_v2-${pkgver_mavlink}"
  aossldir="android_openssl-${pkgver_aossl}"
  gstdir="gst-plugins-good-${pkgver_gst}"

  # Copy in the GPS source
  rm -r "${srcdir}/${pkgname}-${pkgver}/src/GPS/Drivers"
  mkdir -p "${srcdir}/${pkgname}-${pkgver}/src/GPS"
  cp -R "${srcdir}/${gpsdir}" "${srcdir}/${pkgname}-${pkgver}/src/GPS/Drivers"

  # Copy in the mavlink source
  rm -r "${srcdir}/${pkgname}-${pkgver}/libs/mavlink/include/mavlink/v2.0"
  mkdir -p "${srcdir}/${pkgname}-${pkgver}/libs/mavlink/include/mavlink"
  cp -R "${srcdir}/${mavlinkdir}" "${srcdir}/${pkgname}-${pkgver}/libs/mavlink/include/mavlink/v2.0"

  # Copy in the android openssl source
  rm -r "${srcdir}/${pkgname}-${pkgver}/libs/OpenSSL/android_openssl"
  mkdir -p "${srcdir}/${pkgname}-${pkgver}/libs/OpenSSL"
  cp -R "${srcdir}/${aossldir}" "${srcdir}/${pkgname}-${pkgver}/libs/OpenSSL/android_openssl"

  # Copy in the GST source
  rm -r "${srcdir}/${pkgname}-${pkgver}/libs/qmlglsink/gst-plugins-good"
  mkdir -p "${srcdir}/${pkgname}-${pkgver}/libs"
  cp -R "${srcdir}/${gstdir}" "${srcdir}/${pkgname}-${pkgver}/libs/qmlglsink/gst-plugins-good"

  cd "${srcdir}/${pkgname}-${pkgver}"
  patch --strip=1 < "${srcdir}/libicudata-qgc.patch"
}

build() {
  mkdir -p "${srcdir}/${pkgname}-${pkgver}/build"

  cd "$srcdir/${pkgname}-${pkgver}/build"
  qmake ..
  make
}

package() {

  mkdir -p "${pkgdir}/opt" "${pkgdir}/usr/bin" "${pkgdir}/usr/share/applications"
  cp -r "${srcdir}/${pkgname}-${pkgver}/build/staging" "${pkgdir}/opt/${pkgname}"
  cp "${srcdir}/${pkgname}-${pkgver}/resources/icons/qgroundcontrol.png" "${pkgdir}/opt/${pkgname}"
  cp "${srcdir}/${pkgname}-${pkgver}/deploy/qgroundcontrol-start.sh" "${pkgdir}/opt/${pkgname}"

  # Use our own desktop file and remove the default one

  echo "[Desktop Entry]
Type=Application
Name=QGroundControl Release
Comment=Ground control for unmanned vehicles
Path=/opt/${pkgname}/
Exec=/usr/bin/${pkgname}
Icon=/opt/${pkgname}/qgroundcontrol.png
Terminal=false
Categories=Qt;Utility;" > "$srcdir/${pkgname}.desktop"

  rm "${pkgdir}/opt/${pkgname}/${pkgname}.desktop"
  cp "${srcdir}/${pkgname}.desktop" "${pkgdir}/opt/${pkgname}"

  ln -s "/opt/${pkgname}/QGroundControl" "${pkgdir}/usr/bin/${pkgname}"
  ln -s "/opt/${pkgname}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
