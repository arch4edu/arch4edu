# Maintainer: Jingbei Li <i@jingbei.li>
pkgname=phonetisaurus-git
pkgver=r149.e3e2db1
pkgrel=1
pkgdesc="WFST-driven grapheme-to-phoneme (g2p) framework suitable for rapid development of high quality g2p or p2g systems."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/AdolfVonKleist/Phonetisaurus"
license=('BSD')
depends=('openfst' 'python2')
makedepends=('python2-setuptools' 'git')
source=("git+https://github.com/AdolfVonKleist/Phonetisaurus.git")
sha256sums=('SKIP')
provides=('phonetisaurus')
conflicts=('phonetisaurus')

pkgver() {
	cd "$srcdir/Phonetisaurus"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
		-e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
		-e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
		-i $(find $srcdir/Phonetisaurus/python -name '*.py') $srcdir/Phonetisaurus/src/scripts/phonetisaurus-{train,apply}

	cd "$srcdir/Phonetisaurus"
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--enable-python
	make
}

package() {
	cd "$srcdir/Phonetisaurus/python"
	cp ../.libs/Phonetisaurus.so .
	python2 setup.py install --root="$pkgdir"/ --optimize=1

	cd "$srcdir/Phonetisaurus"
	make DESTDIR=${pkgdir} install

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
