# Maintainer: Martin Diehl <https://martin-diehl.net>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>
# Contributor: Simon Pintarelli <simon.pintarelli@gmail.com>
# Contributor: Feng Wang <wanng.fenng@gmail.com>
pkgname=trilinos
pkgver=12.18.1
_pkgver=${pkgver//./-}
pkgrel=4
pkgdesc="algorithms for the solution of large-scale scientific problems"
arch=('x86_64')
url="http://trilinos.org"
license=('LGPL3')
depends=('boost' 'gtest' 'hdf5-openmpi' 'lapack' 'libmatio' 'libx11' 'netcdf' 'python')
makedepends=('gcc-fortran' 'perl' 'blas' 'cmake' 'doxygen' 'bc')
provides=(zoltan)
conflicts=('zoltan')
checkdepends=('cmake')
source=("https://github.com/trilinos/Trilinos/archive/trilinos-release-$_pkgver.tar.gz"
        "Makefile.kokkos.patch"
        "Seacas.patch")
sha256sums=('7d2e1a1a3e2d2ce78bb248c63411fed5007c827b87cadee3d13c1a45c09a89cf'
            '64130011dd70f3be1133c0943be458a7d4f6fa799e6bd47a6294300ced6d827a'
            '0e2f10b7ba4f7349eaef6bff111cda6de6d216ab417e24bf0b342711117a7374')

prepare() {
    cd Trilinos-trilinos-release-12-18-1
    patch --forward --strip=1 --input="${srcdir}/Makefile.kokkos.patch"
    patch --forward --strip=1 --input="${srcdir}/Seacas.patch"
}


build() {
    cd Trilinos-trilinos-release-"$_pkgver"
    mkdir -p build
    cd build

export FFLAGS+=" -fallow-argument-mismatch -fallow-invalid-boz"
export CFLAGS+=" -fcommon"
    cmake .. -DTrilinos_ENABLE_ALL_OPTIONAL_PACKAGES:BOOL=ON \
             -DTrilinos_ENABLE_ALL_PACKAGES:BOOL=ON \
             -DTrilinos_ENABLE_PyTrilinos:BOOL=OFF \
             -DTrilinos_ENABLE_Gtest:BOOL=ON \
             -DTrilinos_ENABLE_TESTS=OFF \
             -DTPL_ENABLE_gtest:BOOL=ON \
             -DTPL_ENABLE_MPI:BOOL=ON \
             -DTPL_ENABLE_HDF5:BOOL=ON \
             -DCMAKE_INSTALL_PREFIX:PATH=/usr \
             -DBUILD_SHARED_LIBS:BOOL=ON
    make VERBOSE=1
}

check() {
    cd Trilinos-trilinos-release-"$_pkgver"/build
    ctest
}

package() {
    cd Trilinos-trilinos-release-"$_pkgver"/build
    make DESTDIR="$pkgdir" install
rm $pkgdir/usr/lib/libgtest.so
rm -rf $pkgdir/usr/include/gtest
}
