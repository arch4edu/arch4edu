# Maintainer Torsten Ke√üler <t dot kessler at posteo dot de>
pkgname=rocclr
pkgver=3.5.0
pkgrel=4
pkgdesc='Radeon Open Compute Common Language Runtime'
arch=('x86_64')
url='https://github.com/ROCm-Developer-Tools/ROCclr'
license=('unknown')
depends=('mesa' 'comgr' 'hsa-rocr' 'hsakmt-roct' 'rocm-cmake')
makedepends=('cmake')
_opencl='https://github.com/RadeonOpenCompute/ROCm-OpenCL-Runtime'
source=("$pkgname-$pkgver.tar.gz::$url/archive/roc-$pkgver.tar.gz"
        "$pkgname-opencl-$pkgver.tar.gz::$_opencl/archive/roc-$pkgver.tar.gz"
	'opencl_includes.patch::https://patch-diff.githubusercontent.com/raw/ROCm-Developer-Tools/ROCclr/pull/16.patch')
sha256sums=('87c1ee9f02b8aa487b628c543f058198767c474cec3d21700596a73c028959e1'
            '511b617d5192f2d4893603c1a02402b2ac9556e9806ff09dd2a91d398abf39a0'
	    '3edeb8aeaf335297ec0f61a15b99c259d607d8f534173fbc3d17832ad03cd63f')
_dirname="$(basename "$url")-$(basename "${source[0]}" .tar.gz)"

prepare() {
	cd "$_dirname"
	patch -Np1 -i "$srcdir/opencl_includes.patch"
}

build() {
    cmake -Wno-dev -B build \
	-S "$srcdir/$_dirname" \
	-DCMAKE_INSTALL_PREFIX='/opt/rocm/rocclr' \
        -DOPENCL_DIR="$srcdir/ROCm-OpenCL-Runtime-roc-$pkgver"

    make -C build
}

package() {
    DESTDIR="$pkgdir" make -C build install

    sed -i "s@$srcdir/build/libamdrocclr_static.a@/opt/rocm/rocclr/lib/libamdrocclr_static.a@" \
        "$srcdir/build/amdrocclr_staticTargets.cmake"
    install -Dm644 "$srcdir/build/amdrocclr_staticTargets.cmake" \
        "$pkgdir/opt/rocm/rocclr/lib/amdrocclr_staticTargets.cmake"
}
