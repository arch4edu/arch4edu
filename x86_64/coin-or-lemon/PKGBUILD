# maintainer Bernardo Ferrari Mendonca <bernardo.mferrari@gmail.com>
pkgname=coin-or-lemon
pkgver=1.3.1
pkgrel=3
pkgdesc="A C++ template library providing many common graph algorithms"
arch=('i686' 'x86_64')
url="http://lemon.cs.elte.hu/trac/lemon"
license=('BSD')
depends=('coin-or-cbc' 'coin-or-clp' 'glpk')
makedepends=('cmake')
options=(staticlibs)
source=("http://lemon.cs.elte.hu/pub/sources/lemon-${pkgver}.tar.gz")
sha1sums=('b23d64c21b4b9088ad51e85316964a4a4138f82a')

build() {
  cd "$srcdir/lemon-$pkgver"
  mkdir -p build_static && cd build_static
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLEMON_ENABLE_GLPK=YES -DLEMON_ENABLE_ILOG=NO -DLEMON_ENABLE_COIN=YES \
    -DLEMON_ENABLE_SOPLEX=no -DGHOSTSCRIPT_EXECUTABLE= \
    -DDOXYGEN_EXECUTABLE= -DPYTHON_EXECUTABLE= .. 
  make
  cd ..
  mkdir -p build_shared && cd build_shared
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=TRUE \
    -DLEMON_ENABLE_GLPK=YES -DLEMON_ENABLE_ILOG=NO -DLEMON_ENABLE_COIN=YES \
    -DLEMON_ENABLE_SOPLEX=no -DGHOSTSCRIPT_EXECUTABLE= \
    -DDOXYGEN_EXECUTABLE= -DPYTHON_EXECUTABLE= .. 
  make
  cd ..
}

package() {
  cd "$srcdir/lemon-$pkgver/build_static"
  make DESTDIR="$pkgdir" install
  cd "$srcdir/lemon-$pkgver/build_shared"
  make DESTDIR="$pkgdir" install
}

