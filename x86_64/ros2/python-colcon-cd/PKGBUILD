# Maintainer: Achmad Fathoni<fathoni.id(at)gmail.com>
pkgname=python-colcon-cd
_name=${pkgname:7}
pkgver=0.2.1
pkgrel=3
pkgdesc="A shell function for colcon-core to change the current working directory."
arch=(any)
url="https://pypi.org/project/$_name"
license=('Apache')
depends=('python-colcon-core')
makedepends=('python-setuptools' 'flake8')
source=(
    https://github.com/colcon/colcon-cd/archive/refs/tags/${pkgver}.tar.gz
    https://github.com/myint/scspell/archive/refs/tags/v2.2.tar.gz)
sha256sums=('91f14108b6f9db780fc5a579b9c809e9a8c62b688e81f2267788fd0e12f178dc'
            '04c6a795a9e0ef4571678eb9fc503bcf39106a975691336af778f31a01027082')

build() {
    cd ${srcdir}/${_name}-${pkgver}
    python -m build --wheel --no-isolation
}

check() {
    cd ${srcdir}/${_name}-${pkgver}/test
    export PYTHONPATH="${srcdir}/scspell-2.2"
    ls test*.py | xargs -I {} pytest {}
}

package() {
    cd ${srcdir}/${_name}-${pkgver}
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -D -m644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
makedepends+=("python-build" "python-installer" "python-wheel")
