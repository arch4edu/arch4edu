# Maintainer: Achmad Fathoni<fathoni.id(at)gmail.com>
# Contributor: Tong Chunli<t.cunly at 163 dot com>
pkgname=python-colcon-devtools
_name=${pkgname:7}
pkgver=0.3.0
pkgrel=3
pkgdesc="An extension for colcon-core to provide information about the plugin system."
arch=(any)
url="https://pypi.org/project/colcon-devtools/"
license=('Apache')
depends=('python-colcon-core')
makedepends=('python-setuptools' 'flake8')
source=(
    https://github.com/colcon/colcon-devtools/archive/refs/tags/${pkgver}.tar.gz
    https://github.com/myint/scspell/archive/refs/tags/v2.2.tar.gz
)
sha256sums=(
    '8363080c03e41c1fe4c884955e32a5dbc2be34269d838a97146042b3af91a53d'
    '04c6a795a9e0ef4571678eb9fc503bcf39106a975691336af778f31a01027082')

build() {
    cd ${srcdir}/${_name}-${pkgver}
    python -m build --wheel --no-isolation
}

check() {
    cd ${srcdir}/${_name}-${pkgver}/test
    pytest test_flake8.py
    pytest test_copyright_license.py
    PYTHONPATH=${srcdir}/scspell-2.2 pytest test_spell_check.py
}

package() {
    cd ${srcdir}/${_name}-${pkgver}
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -D -m644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
makedepends+=("python-build" "python-installer" "python-wheel")
