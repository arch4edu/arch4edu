# Maintainer: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Spies <lukas (at) photoqt _dot_ org>
pkgname=python-symengine-0.13
_pkgname=symengine.py
pkgver=0.13.0
pkgrel=1
arch=(x86_64)
pkgdesc='Python wrappers for SymEngine'
url='https://github.com/symengine/symengine.py'
license=(MIT)
depends=(
    gcc-libs
    glibc
    gmp
    gperftools
    libmpc
    mpfr
    python
    symengine-0.13
)
makedepends=(
    boost
    cereal
    cmake
    cython
    git
    llvm
    python-build
    python-installer
    python-setuptools
)
provides=(python-symengine)
conflicts=(python-symengine)
source=(
    git+https://github.com/symengine/symengine.py#tag=v$pkgver
    no-tcmalloc.patch
    cmake.patch
)
b2sums=('7ccdc0f797874626b309acaaca3e10b6cabe78ec90f9a84a81c3880d028209ee8b8ec5cf5a88288fabc2870b99e036e85b9bd50a8c5374e9c34f0fc7e92c9604'
        'f4e0ffdd1a6e887b7d780b10073bc3512c60e314f374e4a12764d8d7b90ec9763fb75c4487bc0aae5a5c13188d736663ea0ba8d7afa8481ee82043f74f6b4b1d'
        'decb9666ff39ee0c3fb4c146801430357fe84690f2f58ff56e63b265e16d9eccffd00a45ddfe2ee255dd5ac6edc3564adfa0b6541b2fa924f4e6bf689bdec300')

prepare() {
    cd $_pkgname
    # https://github.com/symengine/symengine.py/issues/475
    patch -p1 -i ../no-tcmalloc.patch
    # So that build also works using cmake 4.0 from [kde-unstable]
    patch -p1 -i ../cmake.patch
}

build() {
    cd $_pkgname
    python -m build --wheel --no-isolation
}

package() {
    cd $_pkgname
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
makedepends+=("python-numpy")
