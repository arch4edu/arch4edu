# Maintainer: Carlos Aznarán <caznaranl@uni.pe>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jed Brown <jed@jedbrown.org>
# Contributor: George Eleftheriou <eleftg@gmail.com>
# Contributor: mickele
# Contributor: gborzi
# Contributor: abenson
# Contributor: Jakub Klinkovský <j.l.k cat gmx dog com>
_base=METIS
pkgname=${_base,,}
pkgver=5.2.1
pkgrel=4
arch=(x86_64)
pkgdesc="Serial Graph Partitioning and Fill-reducing Matrix Ordering"
url="https://github.com/KarypisLab/${_base}"
license=(Apache)
depends=(glibc gklib)
makedepends=(cmake)
options=(docs)
source=(${_base}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('69127b7a777f9ba14cf399a7122000af9d6094a5f44be600a28384cfc0495c024fb0c6eab37c4473a5763ec1077cd9f30d9bcbb3c952462f6c9ead31c6b9e790')

prepare() {
  cd ${_base}-${pkgver}
  # Increase CMake version
  sed -i 's/2.8/3.10/' CMakeLists.txt
  echo "target_link_libraries(metis PUBLIC \"-lGKlib\")" >>"libmetis/CMakeLists.txt"
  # Avoid crash with illegal instruction
  sed -i 's|march=native|march=nocona|g' conf/gkbuild.cmake
}

build() {
  cd ${_base}-${pkgver}
  make config \
    shared=1 \
    cc=gcc \
    prefix=/usr \
    gklib_path=/usr
}

package() {
  cd ${_base}-${pkgver}
  make install DESTDIR="${pkgdir}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
