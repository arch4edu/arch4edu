# Maintainer: rpkak <rpkak@users.noreply.github.com>
pkgname='libblake3'
pkgver=1.5.2
pkgrel=1
epoch=
pkgdesc="the C implementations of the BLAKE3 cryptographic hash function"
arch=('x86_64')
url="https://blake3.io/"
license=('CC0-1.0 OR Apache-2.0 OR Apache-2.0 WITH LLVM-exception')
depends=(glibc)
makedepends=(cmake)
checkdepends=()
optdepends=()
provides=(libblake3.so)
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
source=("$pkgname-$pkgver.tar.gz::https://github.com/BLAKE3-team/BLAKE3/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('1bc38cd3cc501458054c3bb473e5e00338d6175121424040079025ea305ddef3')
validpgpkeys=()

build() {
    cmake -B "BLAKE3-$pkgver/c/build" -S "BLAKE3-$pkgver/c" -DBUILD_SHARED_LIBS=True -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -Wno-dev
    cmake --build "BLAKE3-$pkgver/c/build"
}

package() {
    DESTDIR="$pkgdir" cmake --install "BLAKE3-$pkgver/c/build"
    install -Dvm644 "BLAKE3-$pkgver/LICENSE_A2" "$pkgdir/usr/share/licenses/$pkgname/LICENSE_A2"
    install -Dvm644 "BLAKE3-$pkgver/LICENSE_A2LLVM" "$pkgdir/usr/share/licenses/$pkgname/LICENSE_A2LLVM"
    install -Dvm644 "BLAKE3-$pkgver/LICENSE_CC0" "$pkgdir/usr/share/licenses/$pkgname/LICENSE_CC0"
}
