# Maintainer: Martin Diehl <aur@martin-diehl.net>
# Contributor: Andreas Bilke <abilke at cosy dot sbg dot ac dot at>
# Contributor: Myles English <myles at rockhead dot biz>
# Contributor: Lucas H. Gabrielli <heitzmann at gmail dot com>
pkgver=3.24.1
pkgrel=1
pkgname=petsc
pkgdesc="Portable, extensible toolkit for scientific computation"
arch=('i686' 'x86_64')
url="https://petsc.org"
license=('BSD-2-Clause')
options=(staticlibs)
depends=('openmpi' 'lapack' 'bison' 'fftw-openmpi' 'gsl' 'hdf5-openmpi' 'hwloc' 'libjpeg-turbo' 'libyaml'
         'netcdf-openmpi' 'pastix' 'scotch' 'suitesparse' 'superlu' 'zfp' 'zlib'
         'python-numpy' 'python-mpi4py')
makedepends=('gcc' 'gcc-fortran' 'cmake' 'cython' 'python-setuptools')
provides=('petsc4py')
optdepends=(
  'hypre: support for HYPRE'
  'kokkos: support Kokkos'
  'metis: support for METIS'
  'mumps: support for MUMPS'
  'parmetis: support for ParMETIS'
  'scalapack: support for ScaLAPACK'
  'superlu_dist: support for SuperLU_DIST'
  'triangle: support for Triangle'
  'zoltan: support for zoltan'
  )

install=petsc.install
source=(http://web.cels.anl.gov/projects/petsc/download/release-snapshots/${pkgname}-${pkgver}.tar.gz
        test_optdepends.sh)
sha512sums=('c6bc69f85e5965f5492eb9a66e045967b6ba04bb894778e0c07f65adb516b78f0cda3754ee012b770f8ab0677b247d72cd67eb1e2952d2bbc89568b13e16763b'
            'cf63f0e47ac547fdc7c485c3302a8a34cf6a028c25fb44fd0372f76096b4d33fbce4e3b061860f55ff5c9a9d691a7602e4e234e6d7b2dad5b6d94e6566b4e9d4')

PETSC_ARCH=linux-c-opt
_install_dir=/opt/petsc/${PETSC_ARCH}

build() {
  cd ${pkgname}-${pkgver}
  export PETSC_DIR=${PWD}
  export PETSC_ARCH=${PETSC_ARCH}

  CONFOPTS="--with-shared-libraries=1 \
            --with-petsc4py=1 \
            --with-mpi-f90module-visibility=0 \
            --with-cmake=0 \
            --with-mpi-dir=/usr \
            --with-fftw=1 \
            --with-gsl=1 \
            --with-hdf5=1 --with-hdf5-fortran-bindings=1 \
            --with-hwloc=1 \
            --with-libjpeg=1 \
            --with-netcdf=1 --with-zlib=1 \
            --with-pastix=1 \
            --with-ptscotch=1 --with-bison=1 --with-ptscotch-lib=[libesmumps.so,libptscotch.so,libptscotcherr.so,libscotch.so,libscotcherr.so] --with-ptscotch-include=/usr/include \
            --with-suitesparse=1 \
            --with-superlu-lib=-lsuperlu --with-superlu-include=/usr/include/superlu \
            --with-yaml=1 \
            --with-zfp=1 \
            $(sh ${srcdir}/test_optdepends.sh)"

  echo './configure' ${CONFOPTS}
  echo 'CFLAGS='${CFLAGS}
  echo 'CXXFLAGS='${CXXFLAGS}
  echo 'FFLAGS='${FFLAGS}
  echo 'LDFLAGS='${LDFLAGS}
  echo 'MAKEFLAGS='${MAKEFLAGS}

  ./configure --prefix=${_install_dir} ${CONFOPTS} \
              "CFLAGS=$CFLAGS" \
              "CXXFLAGS=$CXXFLAGS" \
              "FFLAGS=$FFLAGS" \
              "LDFLAGS=$LDFLAGS" \
              "MAKEFLAGS=$MAKEFLAGS"

  make all
}

package() {
  cd ${pkgname}-${pkgver}
  export PETSC_DIR=${PWD}
  export PETSC_ARCH=${PETSC_ARCH}

  make DESTDIR=${pkgdir} install

  install -Dm 644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

  mkdir -p ${pkgdir}/etc/profile.d
  echo export PETSC_DIR=${_install_dir} > ${pkgdir}/etc/profile.d/petsc.sh
  echo export PYTHONPATH=${_install_dir}/lib:'${PYTHONPATH}' >> ${pkgdir}/etc/profile.d/petsc.sh
  chmod 644 ${pkgdir}/etc/profile.d/petsc.sh

  # show where the shared libraries are
  install -dm 755 ${pkgdir}/etc/ld.so.conf.d/
  echo ${_install_dir}/lib > ${pkgdir}/etc/ld.so.conf.d/petsc.conf

  # install pkgconfig settings
  install -Dm 644 ${PETSC_ARCH}/lib/pkgconfig/PETSc.pc ${pkgdir}/usr/share/pkgconfig/PETSc.pc
}
depends+=("hypre" "mumps" "parmetis-git")
