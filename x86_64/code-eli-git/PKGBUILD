# Maintainer: acxz <akashpatel2008 at yahoo dot com>
# Contributor: Max Devaine <max@devaine.cz>

pkgname=code-eli-git
pkgver=r1008.aecafc2
pkgrel=1
pkgdesc='Collection of C++ libraries that provide a variety of functionalities.'
arch=('i686' 'x86_64')
url='https://github.com/ramcdona/Code-Eli'
license=('EPL 1.0')
depends=('eigen')
optdepends=('cpptest: unit tests'
            'doxygen: documentation')
makedepends=('cmake' 'git')
_name=Code-Eli
provides=('code-eli')
source=("git+https://github.com/ramcdona/Code-Eli.git"
        "exponent.patch::https://patch-diff.githubusercontent.com/raw/ramcdona/Code-Eli/pull/9.patch")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "$_name"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_name}"
  patch --forward --strip=1 --input="${srcdir}/exponent.patch"
}

build() {

  # Create a build directory
  mkdir -p "${srcdir}/${_name}/build"
  cd "${srcdir}/${_name}/build"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr'

  make
}

package() {

  cd "${srcdir}/${_name}"

  mkdir -p "${pkgdir}/usr/include/eli"
  # include
  cp -r include/eli/* ${pkgdir}/usr/include/eli

  cd "${srcdir}/${_name}/build"
  cp -r include/eli/* ${pkgdir}/usr/include/eli
}
makedepends+=("doxygen")
