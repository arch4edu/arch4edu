# Maintainer: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
_pkgname=multitasking
pkgname=python-multitasking
pkgver=0.0.11
pkgrel=8
pkgdesc="Non-blocking Python methods using decorators"
arch=(any)
url=https://github.com/ranaroussi/multitasking
license=(Apache-2.0)
depends=(python)
makedepends=(
    python-build
    python-installer
    python-setuptools
    python-wheel
)
source=($_pkgname-$pkgver.tar.gz::https://github.com/ranaroussi/$_pkgname/archive/refs/tags/$pkgver.tar.gz)
b2sums=('7c6c5e7270c3edb9535406399d9c8a2fbc45d3a57854c77b27af6375ec4c3cea32fe9c57252916da453b2c47ae23f4f9faf186f54fa651dece130eb31abbb7f3')

build() {
    cd $_pkgname-$pkgver
    python -m build --wheel --no-isolation
}

package() {
    cd $_pkgname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
}
