# Maintainer: Thibaud Kehler <thibaud.kehler at gmx dot net>
pkgname='fet-timetabling'
_module='fet'
pkgver=7.7.4
pkgrel=1
pkgdesc="A software for automatically scheduling the timetable of a school, high-school or university."
arch=('x86_64' 'i686')
url="https://lalescu.ro/liviu/fet/"
license=('AGPL3')
depends=('qt6-base' 'hicolor-icon-theme')
makedepends=('cmake')
source=("https://lalescu.ro/liviu/fet/download/fet-${pkgver}.tar.xz")
sha256sums=('2f5737c6c240afad2ae1134143737c1a4fd7b94e18d786e0a31c47e82041f048')

build() {
	cd "$srcdir/$_module-$pkgver"
	mkdir -p build
	cd build
	cmake .. \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_SYSTEM_LOCALE=ON \
		-DCMAKE_INSTALL_DATADIR=/usr/share
	make
}

package() {
	cd "$srcdir/$_module-$pkgver/build"
	make DESTDIR="${pkgdir}" install
}
options+=("!lto")
