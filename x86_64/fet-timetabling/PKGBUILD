# Maintainer: Thibaud Kehler <thibaud.kehler at gmx dot net>
pkgname='fet-timetabling'
_module='fet'
pkgver=7.7.7
pkgrel=1
pkgdesc="A software for automatically scheduling the timetable of a school, high-school or university."
arch=('x86_64')
url="https://lalescu.ro/liviu/fet/"
license=('AGPL3')
depends=('qt6-base' 'hicolor-icon-theme')
makedepends=('cmake')
source=("https://lalescu.ro/liviu/fet/download/fet-${pkgver}.tar.xz")
sha256sums=('e2fddddb5faa76fb6e90c2263d3ecc6ae8b489f6ea0f19d772e1b54ec281a947')

build() {
	cd "$srcdir/$_module-$pkgver"
	# Use qt-cmake as recommended by the author
	mkdir -p build
	cd build
	/usr/lib/qt6/bin/qt-cmake .. \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_SYSTEM_LOCALE=ON \
		-DCMAKE_INSTALL_DATADIR=/usr/share
	cmake --build .
}

package() {
	cd "$srcdir/$_module-$pkgver/build"
	
	find .qt/ -name "deploy_fet_*.cmake" -exec truncate -s 0 {} + 2>/dev/null || true

	DESTDIR="${pkgdir}" cmake --install . --prefix /usr
}
options+=("!lto")
