#  Maintainer: Blair Bonnett <blair dot bonnett at gmail dot com>
# Contributor: Quan Guo <guotsuan at gmail dot com>

pkgname=python-llvmlite-git
_gitname=llvmlite
pkgver=0.45.0dev0.r328.g942e98b4
pkgrel=1
pkgdesc="A lightweight LLVM Python binding for writing JIT compilers (Git version)"
url="https://github.com/numba/llvmlite"
arch=('i686' 'x86_64')
license=('BSD-2-Clause')
depends=('python' 'llvm15-libs')
makedepends=(
  'git' 'llvm15' 'python-build' 'python-installer'
  'python-setuptools' 'python-wheel'
)
optdepends=(
  'python-graphviz: visualising control-flow graphs'
)
provides=("python-llvmlite=$pkgver")
conflicts=('python-llvmlite')
source=(${_gitname}::git+https://github.com/numba/llvmlite.git)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  LLVM_CONFIG=/usr/bin/llvm-config-15 python -m build --no-isolation --wheel
}

check() {
  cd "$srcdir/$_gitname"
  python runtests.py
}

package() {
  cd "${srcdir}/${_gitname}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove tests from final package.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -r "$pkgdir/$site_packages/llvmlite/tests"
}
