pkgname=devtools-arch4edu-extra
pkgver=26.de0ef8a
pkgrel=1
pkgdesc='Extra tools for arch4edu package maintainers'
arch=('x86_64' 'aarch64')
url='http://github.com/arch4edu/devtools-arch4edu-extra'
license=('GPL')
depends=('devtools')
makedepends=('git')
source=("git+${url}.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	echo "$(git rev-list --count master).$(git rev-parse --short master)"
}

package() {
	cd "$srcdir/$pkgname"
	mkdir -p "$pkgdir/usr/bin"
	mkdir -p "$pkgdir/usr/share/devtools/makepkg.conf.d"
	mkdir -p "$pkgdir/usr/share/devtools/pacman.conf.d"

	case "$CARCH" in
		x86_64)
			cat /usr/share/devtools/pacman.conf.d/extra.conf arch4edu.conf > "$pkgdir/usr/share/devtools/pacman.conf.d/arch4edu.conf"
			cat /usr/share/devtools/pacman.conf.d/multilib.conf arch4edu.conf > "$pkgdir/usr/share/devtools/pacman.conf.d/multilib-arch4edu.conf"
			ln -sf /usr/bin/archbuild "$pkgdir/usr/bin/arch4edu-x86_64-build"
			ln -sf /usr/bin/archbuild "$pkgdir/usr/bin/multilib-arch4edu-build"
			;;
		aarch64)
			cp "$srcdir/$pkgname/pacman-extra-aarch64.conf" "$pkgdir/usr/share/devtools/pacman.conf.d/extra-aarch64.conf"
			cp "$srcdir/$pkgname/makepkg-aarch64.conf" "$pkgdir/usr/share/devtools/makepkg.conf.d/aarch64.conf"
			cat "$srcdir/$pkgname/pacman-extra-aarch64.conf" arch4edu.conf > "$pkgdir/usr/share/devtools/pacman.conf.d/arch4edu-aarch64.conf"
			ln -sf /usr/bin/archbuild "$pkgdir/usr/bin/extra-aarch64-build"
			ln -sf /usr/bin/archbuild "$pkgdir/usr/bin/arch4edu-aarch64-build"
			;;
		*)
			error "Unsupported architecture: $CARCH"
			exit 1
	esac
}
