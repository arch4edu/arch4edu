# Maintainer: Médéric Boquien <mboquien@free.fr>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: Kashyap Chhatbar <kashyap.cc@gmail.com>

_pkg=shap
pkgname=python-${_pkg}
pkgver=0.47.2
pkgrel=1
pkgdesc="Game theoretic approach to explain the output of any machine learning model"
arch=(x86_64)
url="https://github.com/shap/shap"
license=(MIT)
depends=(python python-numpy python-scipy python-scikit-learn python-pandas python-tqdm python-packaging python-slicer python-numba python-cloudpickle)
makedepends=(python-build python-installer python-wheel cython python-setuptools)
optdepends=(ipython python-matplotlib)
source=(https://files.pythonhosted.org/packages/source/s/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha512sums=('5e30944eed87d806fb3a14a2820e1aa2d38bc6e49dc1be909b1d36b61be9eea5877f12e1a19f7d12ba40fac4cfef19b17cc0fcbc23584327a11a24f86cf7a99f')

build() {
  cd ${_pkg}-${pkgver}
  python -m build --wheel --no-isolation --skip-dependency-check
}

package(){
    cd "${_pkg}-${pkgver}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -D LICENSE "${pkgdir}/usr/share/licenses/python-${_pkg}/LICENSE"
}
