# Maintainer: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Conrad Sachweh <conrad+aur@spamthis.space>
pkgname=python-fastdtw
_name=fastdtw
pkgver=0.3.4
pkgrel=8
pkgdesc="Python implementation of FastDTW"
url=https://github.com/slaypni/fastdtw
arch=(x86_64)
license=(MIT)
depends=(
    gcc-libs
    python-numpy
)
makedepends=(
    cython
    python-build
    python-installer
    python-setuptools
    python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
b2sums=('e3a1e630b615c1e074eb7cfa85d3b3173af3a8cde84a29a4454839a42a349212b638709c2af47aec48cdb3fb52c26282bbdf7c5b0f01de17bdd1c660f66c06b8')

build() {
    cd $_name-$pkgver
    python -m build --wheel --no-isolation
}

check() {
    local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
    cd $_name-$pkgver
    python -m installer --destdir=test_dir dist/*.whl
    PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" pytest
}

package() {
    cd $_name-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -D -m644 README.rst "$pkgdir"/usr/share/doc/$pkgname/README
}
