# Maintainer: Frank Siegert <frank.siegert@googlemail.com>
# Maintainer: Vekhir <vekhir at yahoo dot com>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>

## Configuration env vars:
((ENABLE_QT5)) && QT_VER="5" || QT_VER="6"
qt="qt${QT_VER}"

pkgname=openboard
pkgver=1.7.1
_src_folder="OpenBoard-${pkgver}"
pkgrel=4
pkgdesc="Interactive whiteboard software for schools and universities"
arch=('x86_64' 'i686')
url="http://openboard.ch/index.en.html"
license=('GPL3')
# qt{5,6} libraries probed with `ldd -r /opt/openboard/OpenBoard` for both builds
#             qt5-base qt5-declarative qt5-location qt5-multimedia                 qt5-svg qt5-webchannel qt5-webengine
# qt6-5compat qt6-base qt6-declarative              qt6-multimedia qt6-positioning qt6-svg qt6-webchannel qt6-webengine
depends+=(${qt}-{base,declarative,multimedia,svg,webchannel,webengine})
depends+=('openssl' 'ffmpeg')
depends+=(quazip-${qt})  #drop internal quazip and use system one.
depends+=(poppler) #replace internal xpdf with poppler and drop freetype/xpdf from deps
makedepends=('cmake' ${qt}-tools)
source=("https://github.com/OpenBoard-org/OpenBoard/archive/v${pkgver}.tar.gz"
        "openboard-c++20-support.patch::https://github.com/OpenBoard-org/OpenBoard/commit/6a0be1b4607da3c3eb9b7d8b547e7b489bc2d219.patch"
        "openboard-use-c++20.patch::https://github.com/OpenBoard-org/OpenBoard/commit/ffeea1b662b012bd25a025f2130fa2c2044919f9.patch"
        "openboard-add-ffmpeg-7-support.patch::https://patch-diff.githubusercontent.com/raw/OpenBoard-org/OpenBoard/pull/1017.patch")
sha256sums=('5c9fcb54bc1598b4b7026e6ecca07137660dd3d45bda472a5710acf600a2a22f'
            'ac8b4a37192454781216cf38b2fa702d3548a61b5e1c5ce419aa98bcdea77312'
            '8a0765cbc45f3e0e1c78f82d6c627afbb85bf17fbe1e256fec6e1c9ea2fd9313'
            'd39d06b6b9152103efb6983c13f7d258b1eedd8e07d823a718ea816c849ba21d')

prepare() {
  cd "$srcdir"/$_src_folder
  echo "Add support for C++20"
  patch -p1 < "$srcdir"/openboard-c++20-support.patch
  echo "Enable use of C++20"
  patch -p1 < "$srcdir"/openboard-use-c++20.patch
  echo "Add support for FFmpeg 7.0"
  patch -p1 < "$srcdir"/openboard-add-ffmpeg-7-support.patch
}

build() {
  cmake -B build -S "$srcdir"/$_src_folder \
    -DCMAKE_BUILD_TYPE=None \
    -DQT_VERSION=${QT_VER} \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_CXX_STANDARD=20 \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
makedepends+=("icu")
