# Maintainer: Maxim Kurnosenko <asusx2@mail.ru>
# Co-Maintainer: William Tang <ttan0037@student.monash.edu>
# Contributor: RonaldMcDaddy <wannes.demeyer@protonmail.com>
# Contributor: Tinh Truong <xuantinh at gmail dot com>
# Contributor: Cedric Sougne <cedric@sougne.name>
# Contributor: untseac
# Contributor: siasia
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: JuliusTZM <julius dot tzm at gmail dot com>

pkgname=oracle-xe
pkgver=18.4.0.0.0
pkgrel=2
pkgdesc='Oracle Database Express Edition'
url='https://www.oracle.com/database/'
license=('custom')
arch=('x86_64')
depends=('bzip2' 'expat' 'gcc-libs' 'glibc' 'libaio')

source=('https://download.oracle.com/otn-pub/otn_software/db-express/oracle-database-xe-18c-1.0-1.x86_64.rpm'
		'oracle_env.csh'
		'oracle_env.sh'
		'oracle-xe.install'
		'oracle-xe.service')
sha256sums=('4df0318d72a0b97f5468b36919a23ec07533f5897b324843108e0376566d50c8'
			'5472f8ea19a2b33780146bc95e5dc9263bb9c05b18150d24d08a85f04ed86b84'
			'9b584b6403ec0a3c8567d1720347769c92ba9f02ee460979506d641a5aa32f8b'
			'bc5cb49b6e8809b5f69aaef144b23d3a00e00cd10dbadee22d6b8adffb1d4872'
			'332a48069e5019ef5b9efb2308da2738e37d46d848b7e0f993f76a125c48c5fc')

options=('!strip')
install='oracle-xe.install'

package() {
	mv opt usr $pkgdir/

	# Install Environment Files
	install -D -m 744 oracle_env.csh $pkgdir/etc/profile.d/oracle_env.csh
	install -D -m 744 oracle_env.sh $pkgdir/etc/profile.d/oracle_env.sh

	# Install Systemd Service File
	install -D -m 644 oracle-xe.service $pkgdir/usr/lib/systemd/system/oracle-xe.service

	# Create oratab File
	touch $pkgdir/etc/oratab

	# Create oracle Home Directory
	mkdir -p $pkgdir/var/lib/oracle
}
