# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Aaron Lindsay <aaron@aclindsay.com>
# Contributor: Edvinas Valatka <edacval@gmail.com>
# Contributor: Adrian HÃ¼hn <adrian.huehn@web.de>

pkgname='libsearpc'
epoch=2
pkgver=3.3.0
pkgrel=6
pkgdesc="A simple C language RPC framework (including both server side & client side)"
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="https://github.com/haiwen/libsearpc"
license=('Apache')
depends=(
    'glib2'
    'jansson'
    'python-gobject'
    'python-simplejson'
)
makedepends=('git')
_commit='4ccd1988d8abbacc4bda7a14b5a244010b565d9e'
source=("$pkgname::git+$url#commit=$_commit")
sha256sums=('SKIP')

build () {
    cd "$srcdir/$pkgname"
    ./autogen.sh
    ./configure --prefix=/usr PYTHON='/usr/bin/python'
    make
}

check () {
    cd "$srcdir/$pkgname"
    make check
}

package () {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir" install
}
