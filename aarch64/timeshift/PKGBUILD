# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: librewish <librewish at gmail dot com>
# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: Jay Garcia <morbidj at gmail dot com>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Robert Orzanna <orschiro at gmail dot com>

pkgname=timeshift
pkgver=23.12.2
pkgrel=1
pkgdesc="A system restore utility for Linux"
arch=('x86_64')
url="https://github.com/linuxmint/timeshift"
license=('GPL2')
depends=('libgee' 'json-glib' 'gtk3' 'vte3' 'rsync' 'xapp' 'cronie' 'which')
makedepends=('help2man' 'meson' 'vala')
optdepends=('btrfs-progs: For BTRFS support'
            'grub-btrfs: For BTRFS snapshots in GRUB'
            'xorg-xhost: For authorization on Wayland')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('6b087505916bca1037888d3b9e22bf2ce21e17f5918a28148aef1208b555d9ab')

build() {
	arch-meson "${pkgname}-${pkgver}" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
}
