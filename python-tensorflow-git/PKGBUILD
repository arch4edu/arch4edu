# Maintainer: Adria Arrufat (archdria) <adria.arrufat+AUR@protonmail.ch>
# Contributor: Vladislav Odobesku <positivcheg94@gmail.com>
# Contributor: Julien Deswaef (juego) <juego@requiem4tv.com>

pkgname=python-tensorflow-git
pkgver=1.2.0.rc0.863.g85211a479f
pkgrel=1

pkgdesc="Open source software library for numerical computation using data flow graphs.(added support for NVIDIA GPUs with compute capability 5.2,6.1)"
url="https://tensorflow.org/"
license=('Apache2')

arch=('i686' 'x86_64')

provides=('python-tensorflow')
conflicts=('python-tensorflow')
depends=('python-numpy' 'swig' 'python-wheel' 'python-protobuf')
makedepends=('git' 'python-pip' 'bazel' 'rsync' "cuda" "cudnn")
optdepends=('cuda: GPU support'
            'cudnn: GPU support')
source=("git+https://github.com/tensorflow/tensorflow")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/tensorflow
  git describe --tags | sed 's/-/./g;s/v//;'
}

prepare() {
  cd ${srcdir}/tensorflow

  # clean and create the directory to store the wheel file
  if [ -d ${srcdir}/tmp ]; then
    rm -rf ${srcdir}/tmp
  else
    mkdir -p ${srcdir}/tmp
  fi

  # setup environment variables
  export PYTHON_BIN_PATH=/usr/bin/python
  export USE_DEFAULT_PYTHON_LIB_PATH=1
  if (pacman -Q cuda &>/dev/null && pacman -Q cudnn &>/dev/null); then
    msg2 "CUDA support enabled"
    _build_opts="--config=cuda"
    makedepends+=gcc-5
    export GCC_HOST_COMPILER_PATH=/usr/bin/gcc-5
    export TF_NEED_CUDA=1
    export TF_UNOFFICIAL_SETTING=1
    export CUDA_TOOLKIT_PATH=/opt/cuda
    export CUDNN_INSTALL_PATH=/opt/cuda
    # adapt to your needs
    export TF_CUDA_VERSION=$($CUDA_TOOLKIT_PATH/bin/nvcc --version | sed -n 's/^.*release \(.*\),.*/\1/p')
    export TF_CUDNN_VERSION=$(sed -n 's/^#define CUDNN_MAJOR\s*\(.*\).*/\1/p' $CUDNN_INSTALL_PATH/include/cudnn.h)
    export TF_CUDA_COMPUTE_CAPABILITIES="5.2,6.1"
  else
    msg2 "CUDA support disabled"
    export TF_NEED_CUDA=0
  fi

  # disable Google Cloud Platform support
  export TF_NEED_GCP=0
  # disable Hadoop File System support
  export TF_NEED_HDFS=0
  # disable OpenCL support
  export TF_NEED_OPENCL=0
  # enable XLA JIT compiler
  export TF_ENABLE_XLA=0
  # enable jemalloc support
  export TF_NEED_JEMALLOC=1
  # set up architecture dependent optimization flags
  export CC_OPT_FLAGS="-march=native"
  # use nvcc instead of clang to build CUDA
  export TF_CUDA_CLANG=0
  # disable VERBS support
  export TF_NEED_VERBS=0

  # make sure the proxy variables are in all caps, otherwise bazel ignores them
  export HTTP_PROXY=`echo $http_proxy | sed -e 's/\/$//'`
  export HTTPS_PROXY=`echo $https_proxy | sed -e 's/\/$//'`
}

build() {
  cd ${srcdir}/tensorflow

  yes "" |  ./configure
  bazel build -c opt ${_build_opts} --copt=${CC_OPT_FLAGS} //tensorflow/tools/pip_package:build_pip_package

  msg2 "Building pip package..."
  bazel-bin/tensorflow/tools/pip_package/build_pip_package ${srcdir}/tmp
}

package() {
  cd ${srcdir}/tensorflow

  TMP_PKG=`find $srcdir/tmp -name "tensor*.whl"`
  pip install --ignore-installed --upgrade --root $pkgdir/ $TMP_PKG --no-dependencies
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
